
#:def pow_d(dual_type, dual_sn, real_kind, size_dx, is_pure, test_coverage)
    #:if is_pure and not test_coverage
        #:set elemental_purity = "elemental"
    #:else
        #:set elemental_purity = "impure elemental"
    #:endif
    ${elemental_purity}$ function pow_${dual_sn}$_i(u, v) result(res)
        type(${dual_type}$), intent(in) :: u
        integer, intent(in) :: v
        type(${dual_type}$) :: res
        
        res%f = u%f**v
        res%g = u%f**(v - 1)*u%g*v
      #:if test_coverage
        pow_d_i_counter = pow_d_i_counter + 1
      #:endif
    end function
    ${elemental_purity}$ function pow_${dual_sn}$_r(u, v) result(res)
        type(${dual_type}$), intent(in) :: u
        real(${real_kind}$), intent(in) :: v
        type(${dual_type}$) :: res
        
        res%f = u%f**v
        res%g = u%f**(v - 1)*u%g*v
      #:if test_coverage
        pow_d_r_counter = pow_d_r_counter + 1
      #:endif
    end function
    ${elemental_purity}$ function pow_${dual_sn}$_${dual_sn}$(u, v) result(res)
        type(${dual_type}$), intent(in) :: u
        type(${dual_type}$), intent(in) :: v
        type(${dual_type}$) :: res
        real(${real_kind}$) :: t0
        
        t0 = u%f**v%f
        res%f = t0
        res%g = t0*(u%g*v%f/u%f + v%g*log(u%f))

      #:if test_coverage
        pow_d_d_counter = pow_d_d_counter + 1
      #:endif
    end function
#:enddef

#:def pow_hd(dual_type, dual_sn, real_kind, size_dx, is_pure, test_coverage)
    #:if is_pure and not test_coverage
        #:set elemental_purity = "elemental"
    #:else
        #:set elemental_purity = "impure elemental"
    #:endif
    ${elemental_purity}$ function pow_${dual_sn}$_i(u, v) result(res)
        type(${dual_type}$), intent(in) :: u
        integer, intent(in) :: v
        type(${dual_type}$) :: res
        integer :: i, j, k
        
        res%f = u%f**v
        res%g = u%f**(v - 1)*v*u%g
        k = 0
        do j = 1, ${size_dx}$
            do i = j, ${size_dx}$
                k = k + 1
                res%h(k) = v*(u%g(i)*u%g(j)*(v - 1)*u%f**(v-2) + u%h(k)*u%f**(v-1))
            end do
        end do
      #:if test_coverage
        pow_hd_i_counter = pow_hd_i_counter + 1
      #:endif
    end function
    ${elemental_purity}$ function pow_${dual_sn}$_r(u, v) result(res)
        type(${dual_type}$), intent(in) :: u
        real(${real_kind}$), intent(in) :: v
        type(${dual_type}$) :: res
        integer :: i, j, k
        
        res%f = u%f**v
        res%g = u%f**(v - 1)*v*u%g
        k = 0
        do j = 1, ${size_dx}$
            do i = j, ${size_dx}$
                k = k + 1
                res%h(k) = v*(u%g(i)*u%g(j)*(v - 1)*u%f**(v-2) + u%h(k)*u%f**(v-1))
            end do
        end do
      #:if test_coverage
        pow_hd_r_counter = pow_hd_r_counter + 1
      #:endif
    end function
    ${elemental_purity}$ function pow_${dual_sn}$_${dual_sn}$(u, v) result(res)
        type(${dual_type}$), intent(in) :: u
        type(${dual_type}$), intent(in) :: v
        type(${dual_type}$) :: res
        integer :: i, j, k
        real(${real_kind}$) :: t0,t1,t2,t3
        
        t0 = u%f**v%f
        t1 = log(u%f)
        t2 = 1.0_${real_kind}$/u%f
        t3 = t1*v%f + 1
        res%f = t0
        res%g = t0*(t1*v%g + t2*u%g*v%f)
        k = 0
        do j = 1, ${size_dx}$
            do i = j, ${size_dx}$
                k = k + 1
                res%h(k) = t0*(t1*v%h(k) + t2*u%h(k)*v%f + t2*u%g(j)*(t2*u%g(i)*v%f*(v%f - &
      1) + t3*v%g(i)) + v%g(j)*(t1**2*v%g(i) + t2*t3*u%g(i)))
            end do
        end do
      #:if test_coverage
        pow_hd_hd_counter = pow_hd_hd_counter + 1
      #:endif
    end function
#:enddef


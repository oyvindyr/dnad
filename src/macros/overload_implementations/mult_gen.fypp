
#:def mult_d(dual_type, dual_sn, real_kind, size_dx, is_pure, test_coverage)
    #:if is_pure and not test_coverage
        #:set elemental_purity = "elemental"
    #:else
        #:set elemental_purity = "impure elemental"
    #:endif
    ${elemental_purity}$ function mult_${dual_sn}$_${dual_sn}$(u, v) result(res)
        type(${dual_type}$), intent(in) :: u
        type(${dual_type}$), intent(in) :: v
        type(${dual_type}$) :: res
        
        res%f = u%f*v%f
        res%g = u%g*v%f + u%f*v%g
      #:if test_coverage
        mult_d_d_counter = mult_d_d_counter + 1
      #:endif
    end function
    ${elemental_purity}$ function mult_${dual_sn}$_r(u, v) result(res)
        type(${dual_type}$), intent(in) :: u
        real(${real_kind}$), intent(in) :: v
        type(${dual_type}$) :: res
        
        res%f = u%f*v
        res%g = u%g*v
      #:if test_coverage
        mult_d_r_counter = mult_d_r_counter + 1
      #:endif
    end function
    ${elemental_purity}$ function mult_r_${dual_sn}$(u, v) result(res)
        real(${real_kind}$), intent(in) :: u
        type(${dual_type}$), intent(in) :: v
        type(${dual_type}$) :: res
        
        res%f = u*v%f
        res%g = u*v%g
      #:if test_coverage
        mult_r_d_counter = mult_r_d_counter + 1
      #:endif
    end function
    ${elemental_purity}$ function mult_${dual_sn}$_i(u, v) result(res)
        type(${dual_type}$), intent(in) :: u
        integer, intent(in) :: v
        type(${dual_type}$) :: res
        
        res%f = u%f*v
        res%g = u%g*v
      #:if test_coverage
        mult_d_i_counter = mult_d_i_counter + 1
      #:endif
    end function
    ${elemental_purity}$ function mult_i_${dual_sn}$(u, v) result(res)
        integer, intent(in) :: u
        type(${dual_type}$), intent(in) :: v
        type(${dual_type}$) :: res
        
        res%f = u*v%f
        res%g = u*v%g
      #:if test_coverage
        mult_i_d_counter = mult_i_d_counter + 1
      #:endif
    end function
#:enddef

#:def mult_hd(dual_type, dual_sn, real_kind, size_dx, is_pure, test_coverage)
    #:if is_pure and not test_coverage
        #:set elemental_purity = "elemental"
    #:else
        #:set elemental_purity = "impure elemental"
    #:endif
    ${elemental_purity}$ function mult_${dual_sn}$_${dual_sn}$(u, v) result(res)
        type(${dual_type}$), intent(in) :: u
        type(${dual_type}$), intent(in) :: v
        type(${dual_type}$) :: res
        integer :: i, j, k
        
        res%f = u%f*v%f
        res%g = u%g*v%f + u%f*v%g
        k = 0
        do j = 1, ${size_dx}$
            do i = j, ${size_dx}$
                k = k + 1
                res%h(k) = u%h(k)*v%f + u%g(i)*v%g(j) + u%g(j)*v%g(i) + u%f*v%h(k)
            end do
        end do
      #:if test_coverage
        mult_hd_hd_counter = mult_hd_hd_counter + 1
      #:endif
    end function
    ${elemental_purity}$ function mult_${dual_sn}$_r(u, v) result(res)
        type(${dual_type}$), intent(in) :: u
        real(${real_kind}$), intent(in) :: v
        type(${dual_type}$) :: res
        
        res%f = u%f*v
        res%g = u%g*v
        res%h = u%h*v
      #:if test_coverage
        mult_hd_r_counter = mult_hd_r_counter + 1
      #:endif
    end function
    ${elemental_purity}$ function mult_r_${dual_sn}$(u, v) result(res)
        real(${real_kind}$), intent(in) :: u
        type(${dual_type}$), intent(in) :: v
        type(${dual_type}$) :: res
        
        res%f = u*v%f
        res%g = u*v%g
        res%h = u*v%h
      #:if test_coverage
        mult_r_hd_counter = mult_r_hd_counter + 1
      #:endif
    end function
    ${elemental_purity}$ function mult_${dual_sn}$_i(u, v) result(res)
        type(${dual_type}$), intent(in) :: u
        integer, intent(in) :: v
        type(${dual_type}$) :: res
        
        res%f = u%f*v
        res%g = u%g*v
        res%h = u%h*v
      #:if test_coverage
        mult_hd_i_counter = mult_hd_i_counter + 1
      #:endif
    end function
    ${elemental_purity}$ function mult_i_${dual_sn}$(u, v) result(res)
        integer, intent(in) :: u
        type(${dual_type}$), intent(in) :: v
        type(${dual_type}$) :: res
        
        res%f = u*v%f
        res%g = u*v%g
        res%h = u*v%h
      #:if test_coverage
        mult_i_hd_counter = mult_i_hd_counter + 1
      #:endif
    end function
#:enddef


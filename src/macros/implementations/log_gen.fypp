
#:def log_d(dual_type, real_kind, size_dx, is_pure, test_coverage)
    #:if is_pure and not test_coverage
        #:set elemental_purity = "elemental"
    #:else
        #:set elemental_purity = "impure elemental"
    #:endif
    ${elemental_purity}$ function log_d(u) result(res)
        type(${dual_type}$), intent(in) :: u
        type(${dual_type}$) :: res
        
        res%x = log(u%x)
        res%dx = u%dx/u%x
      #:if test_coverage
        log_d_counter = log_d_counter + 1
      #:endif
    end function
#:enddef

#:def log_hd(dual_type, real_kind, size_dx, is_pure, test_coverage)
    #:if is_pure and not test_coverage
        #:set elemental_purity = "elemental"
    #:else
        #:set elemental_purity = "impure elemental"
    #:endif
    ${elemental_purity}$ function log_hd(u) result(res)
        type(${dual_type}$), intent(in) :: u
        type(${dual_type}$) :: res
        integer :: i, j, k
        real(${real_kind}$) :: t0
        
        t0 = 1.0_${real_kind}$/u%x
        res%x = log(u%x)
        res%dx = t0*u%dx
        k = 0
        do j = 1, ${size_dx}$
            do i = j, ${size_dx}$
                k = k + 1
                res%ddx(k) = t0*(-t0*u%dx(i)*u%dx(j) + u%ddx(k))
            end do
        end do
      #:if test_coverage
        log_hd_counter = log_hd_counter + 1
      #:endif
    end function
#:enddef


#:def dnad_jacobi_interfaces(type_specs)
    interface jacobi
        !! Extract Jacobi matrix from a vector of dual or hyper-dual numbers.
      #:for type_spec in type_specs
        #:set type_suffix, has_hdual = dnad.decode_type_spec(type_spec)
        module procedure jacobi__d${type_suffix}$
       #:if has_hdual
        module procedure jacobi__hd${type_suffix}$
       #:endif
      #:endfor
    end interface
    interface jacobi_tr
        !! Extract transpose of Jacobi matrix from a vector of dual or hyper-dual numbers.
      #:for type_spec in type_specs
        #:set type_suffix, has_hdual = dnad.decode_type_spec(type_spec)
        module procedure jacobi_tr__d${type_suffix}$
       #:if has_hdual
        module procedure jacobi_tr__hd${type_suffix}$
       #:endif
      #:endfor
    end interface
    interface jacobi_d
        !! Extract Jacobi matrix from a vector of dual or hyper-dual numbers.
        !! - A hyper-dual vector results in a dual matrix
        !! - A dual vector results in a real matrix (same as jacobi)
      #:for type_spec in type_specs
        #:set type_suffix, has_hdual = dnad.decode_type_spec(type_spec)
        module procedure jacobi__d${type_suffix}$
       #:if has_hdual
        module procedure jacobi_d__hd${type_suffix}$
       #:endif
      #:endfor
    end interface
    interface jacobi_tr_d
        !! Extract transpose of Jacobi matrix from a vector of dual or hyper-dual numbers.
        !! - A hyper-dual vector results in a dual matrix
        !! - A dual vector results in a real matrix (same as jacobi_tr)
      #:for type_spec in type_specs
        #:set type_suffix, has_hdual = dnad.decode_type_spec(type_spec)
        module procedure jacobi_tr__d${type_suffix}$
       #:if has_hdual
        module procedure jacobi_tr_d__hd${type_suffix}$
       #:endif
      #:endfor
    end interface
#:enddef

#:def dnad_jacobi_implementations(type_specs, real_kind)
  #:for type_spec in type_specs
    #:set type_suffix, has_hdual = dnad.decode_type_spec(type_spec)
    pure function jacobi__d${type_suffix}$(x) result(y)
        type(dual__${type_suffix}$_t), intent(in) :: x(:)
        real(${real_kind}$) :: y(size(x),size(x(1)%g))

        integer :: j

        do j = 1, size(x)
            y(j,:) = x(j)%g
        end do

    end function
    pure function jacobi_tr__d${type_suffix}$(x) result(y)
        type(dual__${type_suffix}$_t), intent(in) :: x(:)
        real(${real_kind}$) :: y(size(x(1)%g),size(x))

        integer :: j

        do j = 1, size(x)
            y(:,j) = x(j)%g
        end do

    end function
   #:if has_hdual
    pure function jacobi__hd${type_suffix}$(x) result(y)
        type(hdual__${type_suffix}$_t), intent(in) :: x(:)
        real(${real_kind}$) :: y(size(x),size(x(1)%d%g))

        integer :: j

        do j = 1, size(x)
            y(j,:) = x(j)%d%g
        end do

    end function
    pure function jacobi_d__hd${type_suffix}$(x) result(y)
        type(hdual__${type_suffix}$_t), intent(in) :: x(:)
        type(dual__${type_suffix}$_t) :: y(size(x),size(x(1)%d%g))

        real(${real_kind}$) :: hess(size(x(1)%d%g), size(x(1)%d%g))
        integer :: i, j

        do j = 1, size(x)
            hess = hessian(x(j))
            do i = 1, size(x(1)%d%g)
                y(j,i)%f = x(j)%d%g(i)
                y(j,i)%g = hess(:,i)
            end do
        end do

    end function
    pure function jacobi_tr__hd${type_suffix}$(x) result(y)
        type(hdual__${type_suffix}$_t), intent(in) :: x(:)
        real(${real_kind}$) :: y(size(x(1)%d%g),size(x))

        integer :: j

        do j = 1, size(x)
            y(:,j) = x(j)%d%g
        end do

    end function
    pure function jacobi_tr_d__hd${type_suffix}$(x) result(y)
        type(hdual__${type_suffix}$_t), intent(in) :: x(:)
        type(dual__${type_suffix}$_t) :: y(size(x(1)%d%g),size(x))

        real(${real_kind}$) :: hess(size(x(1)%d%g), size(x(1)%d%g))
        integer :: i, j

        do j = 1, size(x)
            hess = hessian(x(j))
            do i = 1, size(x(1)%d%g)
                y(i,j)%f = x(j)%d%g(i)
                y(i,j)%g = hess(:,i)
            end do
        end do

    end function
   #:endif
  #:endfor
#:enddef

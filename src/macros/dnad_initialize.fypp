    
#:def dnad_initialize_interface(type_specs)
    interface initialize 
        !! Initialize a dual or hyper dual number
      #:for type_spec in type_specs
        #:set type_suffix, has_hdual = dnad.decode_type_spec(type_spec)
        module procedure initialize__d_${type_suffix}$_scalar
        module procedure initialize__d_${type_suffix}$_vector
       #:if has_hdual
        module procedure initialize__hd_${type_suffix}$_scalar
        module procedure initialize__hd_${type_suffix}$_vector
       #:endif
      #:endfor
    end interface
#:enddef

#:def dnad_initialize_implementations(type_specs, real_kind)
  #:for type_spec in type_specs
    #:set type_suffix, has_hdual = dnad.decode_type_spec(type_spec)
    pure subroutine initialize__d_${type_suffix}$_scalar(dual, val, idiff)
        !! Initialize a single dual number, whose derivative with respect to design variable 'idiff' is 1
        type(dual__${type_suffix}$_t), intent(out) :: dual
        real(${real_kind}$), intent(in) :: val
        integer, intent(in) :: idiff
        
        dual%f = val
        dual%g = 0
        dual%g(idiff) = 1

    end subroutine
    pure subroutine initialize__d_${type_suffix}$_vector(dual, val)
        !! Initialize a vector of dual numbers, where the derivative of 
        !! number i with respect to design variable i is 1
        type(dual__${type_suffix}$_t), intent(out) :: dual(:)
        real(${real_kind}$), intent(in) :: val(:)

        integer :: i

        do i = 1, size(dual)
            dual(i)%f = val(i)
            dual(i)%g = 0
            dual(i)%g(i) = 1
        end do

    end subroutine
   #:if has_hdual
    pure subroutine initialize__hd_${type_suffix}$_scalar(hdual, val, idiff)
        !! Initialize a single hyper-dual number, whose derivative with respect to design variable 'idiff' is 1
        type(hdual__${type_suffix}$_t), intent(out) :: hdual
        real(${real_kind}$), intent(in) :: val
        integer, intent(in) :: idiff
        
        hdual%d%f = val
        hdual%d%g = 0
        hdual%h = 0
        hdual%d%g(idiff) = 1

    end subroutine
    pure subroutine initialize__hd_${type_suffix}$_vector(hdual, val)
        !! Initialize a vector of hyper-dual numbers, where the derivative of 
        !! number i with respect to design variable i is 1
        type(hdual__${type_suffix}$_t), intent(out) :: hdual(:)
        real(${real_kind}$), intent(in) :: val(:)

        integer :: i
        
        do i = 1, size(hdual)
            hdual(i)%d%f = val(i)
            hdual(i)%d%g = 0
            hdual(i)%h = 0
            hdual(i)%d%g(i) = 1
        end do

    end subroutine
   #:endif
  #:endfor
#:enddef

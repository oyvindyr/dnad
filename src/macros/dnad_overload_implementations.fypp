#! Include implementations from the macros/overload_implementations directory.
#! File names ending with _gen.fypp are generated by Python from Sympy expressions
#!
#:include 'assign.fypp'
#:include 'add_gen.fypp'
#:include 'minus_gen.fypp'
#:include 'mult_gen.fypp'
#:include 'div_gen.fypp'
#:include 'pow.fypp'
#:include 'eq.fypp'
#:include 'le.fypp'
#:include 'lt.fypp'
#:include 'ge.fypp'
#:include 'gt.fypp'
#:include 'ne.fypp'

#:include 'log_gen.fypp'
#:include 'sqrt.fypp'

#!
#!
#:def dnad_overload_implementations(type_specs, real_kind, selected_interfaces="--all", stringent=True, is_pure=True, test_coverage=False) 
#! 
#:set interface_list = dnad.select_interfaces(selected_interfaces)
#:set dual_types, dual_shortnames, hdual_types, hdual_shortnames = dnad.decode_type_specs(type_specs)
#!
#:if is_pure and not test_coverage
    #:set elemental_purity = "elemental"
    #:set purity = "pure"
#:else
    #:set elemental_purity = "impure elemental"
    #:set purity = ""
#:endif
#!
#!
#:if "=" in interface_list 
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    $:assign_d(dual_type, dual_sn, real_kind, is_pure, test_coverage)
  #:endfor
  #:for hdual_type, hdual_sn in zip(hdual_types, hdual_shortnames)
    $:assign_hd(hdual_type, hdual_sn, real_kind, is_pure, test_coverage)
  #:endfor
#:endif 
#!
#!
#:if "+" in interface_list
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    $:add_d( dual_type, dual_sn, real_kind, is_pure, test_coverage)
  #:endfor
  #:for hdual_type, hdual_sn in zip(hdual_types, hdual_shortnames)
    $:add_hd(hdual_type, hdual_sn, real_kind, is_pure, test_coverage)
  #:endfor
#:endif
#!
#! 
#:if "-" in interface_list 
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    $:minus_d( dual_type, dual_sn, real_kind, is_pure, test_coverage)
  #:endfor
  #:for hdual_type, hdual_sn in zip(hdual_types, hdual_shortnames)
    $:minus_hd(hdual_type, hdual_sn, real_kind, is_pure, test_coverage)
  #:endfor
#:endif 
#!
#! 
#:if "*" in interface_list 
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    $:mult_d(dual_type, dual_sn, real_kind, is_pure, test_coverage)
  #:endfor
  #:for hdual_type, hdual_sn in zip(hdual_types, hdual_shortnames)
    $:mult_hd(hdual_type, hdual_sn, real_kind, is_pure, test_coverage)
  #:endfor
#:endif 
#!
#! 
#:if "/" in interface_list 
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    $:div_d(dual_type, dual_sn, real_kind, is_pure, test_coverage)
  #:endfor
  #:for hdual_type, hdual_sn in zip(hdual_types, hdual_shortnames)
    $:div_hd(hdual_type, hdual_sn, real_kind, is_pure, test_coverage)
  #:endfor
#:endif 
#!
#! 
#:if "**" in interface_list 
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    $:pow_d(dual_type, dual_sn, real_kind, is_pure, test_coverage)
  #:endfor
  #:for hdual_type, hdual_sn in zip(hdual_types, hdual_shortnames)
    $:pow_hd(hdual_type, hdual_sn, real_kind, is_pure, test_coverage)
  #:endfor
#:endif 
#!
#! 
#:if "==" in interface_list 
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    $:eq(dual_type, dual_sn, real_kind, is_pure, False, test_coverage)
  #:endfor
  #:for hdual_type, hdual_sn in zip(hdual_types, hdual_shortnames)
    $:eq(hdual_type, hdual_sn, real_kind, is_pure, True, test_coverage)
  #:endfor
#:endif 
#:if "<=" in interface_list 
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    $:le(dual_type, dual_sn, real_kind, is_pure, False, test_coverage)
  #:endfor
  #:for hdual_type, hdual_sn in zip(hdual_types, hdual_shortnames)
    $:le(hdual_type, hdual_sn, real_kind, is_pure, True, test_coverage)
  #:endfor
#:endif 
#:if "<" in interface_list 
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    $:lt(dual_type, dual_sn, real_kind, is_pure, False, test_coverage)
  #:endfor
  #:for hdual_type, hdual_sn in zip(hdual_types, hdual_shortnames)
    $:lt(hdual_type, hdual_sn, real_kind, is_pure, True, test_coverage)
  #:endfor
#:endif 
#:if ">=" in interface_list 
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    $:ge(dual_type, dual_sn, real_kind, is_pure, False, test_coverage)
  #:endfor
  #:for hdual_type, hdual_sn in zip(hdual_types, hdual_shortnames)
    $:ge(hdual_type, hdual_sn, real_kind, is_pure, True, test_coverage)
  #:endfor
#:endif 
#:if ">" in interface_list 
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    $:gt(dual_type, dual_sn, real_kind, is_pure, False, test_coverage)
  #:endfor
  #:for hdual_type, hdual_sn in zip(hdual_types, hdual_shortnames)
    $:gt(hdual_type, hdual_sn, real_kind, is_pure, True, test_coverage)
  #:endfor
#:endif 
#:if "/=" in interface_list 
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    $:ne(dual_type, dual_sn, real_kind, is_pure, False, test_coverage)
  #:endfor
  #:for hdual_type, hdual_sn in zip(hdual_types, hdual_shortnames)
    $:ne(hdual_type, hdual_sn, real_kind, is_pure, True, test_coverage)
  #:endfor
#:endif 
#:if "abs" in interface_list or "dabs" in interface_list
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    ${elemental_purity}$  function abs__${dual_sn}$(u) result(res)
        type(${dual_type}$), intent(in) :: u
        type(${dual_type}$) :: res
        integer :: i

      #:if stringent == True
        ! We need this!
        if (u%f > 0) then
            res%f = u%f
            res%g = u%g
        else if (u%f < 0) then
            res%f = -u%f
            res%g = -u%g
        else
            res%f = 0.0_${real_kind}$
            do i = 1, size(u%g)
                if (u%g(i) .eq. 0.0_${real_kind}$) then
                    res%g(i) = 0.0_${real_kind}$
                else
                    res%g(i) = set_NaN()
                end if
            end do
        end if
      #:else 
        res%f = abs(u%f)
        res%g = sign(u%g, u%f)
      #:endif

    end function
  #:endfor
#:endif 
#:if "acos" in interface_list 
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    ${elemental_purity}$  function acos__${dual_sn}$(u) result(res)
        type(${dual_type}$), intent(in) :: u
        type(${dual_type}$) :: res

        res%f = acos(u%f)
    
      #:if stringent == True
        if (u%f == 1.0_${real_kind}$ .or. u%f == -1.0_${real_kind}$) then
            res%g = set_Nan()  ! Undefined derivative
        else
            res%g = -u%g / sqrt(1.0_${real_kind}$ - u%f**2)
        end if
      #:else 
        res%g = -u%g / sqrt(1.0_${real_kind}$ - u%f**2)
      #:endif

    end function
  #:endfor
#:endif 
#:if "asin" in interface_list 
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    ${elemental_purity}$  function asin__${dual_sn}$(u) result(res)
        type(${dual_type}$), intent(in) :: u
        type(${dual_type}$) :: res

        res%f = asin(u%f)
      #:if stringent == True
        if (u%f == 1.0_${real_kind}$ .or. u%f == -1.0_${real_kind}$) then
            res%g = set_NaN()  ! Undefined derivative
        else
            res%g = u%g / sqrt(1.0_${real_kind}$ - u%f**2)
        end if
      #:else 
        res%g = u%g / sqrt(1.0_${real_kind}$ - u%f**2)
      #:endif

    end function
  #:endfor
#:endif 
#:if "atan" in interface_list 
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    ${elemental_purity}$  function atan__${dual_sn}$(u) result(res)
        type(${dual_type}$), intent(in) :: u
        type(${dual_type}$) :: res

        res%f = atan(u%f)
        res%g = u%g / (1.0_${real_kind}$ + u%f**2)

    end function
  #:endfor
#:endif 
#:if "atan2" in interface_list 
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    ${elemental_purity}$  function atan2__${dual_sn}$(u, v) result(res)
        type(${dual_type}$), intent(in) :: u, v
        type(${dual_type}$) :: res

        real(${real_kind}$) :: usq_plus_vsq

        res%f = atan2(u%f, v%f)

        usq_plus_vsq = u%f**2 + v%f**2
        res%g = v%f / usq_plus_vsq * u%g - u%f / usq_plus_vsq * v%g

    end function
  #:endfor
#:endif 
#:if "cos" in interface_list or "dcos" in interface_list
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    ${elemental_purity}$  function cos__${dual_sn}$(u) result(res)
        type(${dual_type}$), intent(in) :: u
        type(${dual_type}$) :: res

        res%f = cos(u%f)
        res%g = -sin(u%f) * u%g

    end function
  #:endfor
#:endif 
#:if "dot_product" in interface_list 
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    ${purity}$  function dot_product__${dual_sn}$_${dual_sn}$(u, v) result(res)
        type(${dual_type}$), intent(in) :: u(:), v(:)
        type(${dual_type}$) :: res

        integer :: i

        res%f = dot_product(u%f, v%f)
        do i = 1, size(res%g)
            res%g(i) = dot_product(u%f, v%g(i)) + dot_product(v%f, u%g(i))
        end do

    end function
  #:endfor
#:endif 
#:if "exp" in interface_list 
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    ${elemental_purity}$  function exp__${dual_sn}$(u) result(res)
        type(${dual_type}$), intent(in) :: u
        type(${dual_type}$) :: res

        real(${real_kind}$) :: exp_x

        exp_x = exp(u%f)
        res%f = exp_x
        res%g = u%g * exp_x

    end function
  #:endfor
#:endif 
#:if "int" in interface_list 
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    ${elemental_purity}$  function int__${dual_sn}$(u) result(res)
         type(${dual_type}$), intent(in) :: u
         integer :: res

         res = int(u%f)

    end function
  #:endfor
#:endif 
#:if "log" in interface_list
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    $:log_d(dual_type, dual_sn, real_kind, is_pure, test_coverage)
  #:endfor
  #:for hdual_type, hdual_sn in zip(hdual_types, hdual_shortnames)
    $:log_hd(hdual_type, hdual_sn, real_kind, is_pure, test_coverage)
  #:endfor
#:endif 
#:if "log10" in interface_list
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    ${elemental_purity}$  function log10__${dual_sn}$(u) result(res)
        type(${dual_type}$), intent(in) :: u
        type(${dual_type}$) :: res

        real(${real_kind}$) :: inv

        inv = 1.0_${real_kind}$ / (u%f * log(10.0_${real_kind}$))
        res%f = log10(u%f)
        res%g = u%g * inv

    end function
  #:endfor
#:endif 
#:if "matmul" in interface_list 
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    ${purity}$  function matmul__${dual_sn}$_${dual_sn}$(u,v) result(res)
        type(${dual_type}$), intent(in) :: u(:,:), v(:,:)
        type(${dual_type}$) :: res(size(u,1), size(v,2))

        integer :: i

        res%f = matmul(u%f, v%f)
        do i = 1, size(res(1,1)%g)
            res%g(i) = matmul(u%g(i), v%f) + matmul(u%f, v%g(i))
        end do

    end function
    ${purity}$  function matmul__${dual_sn}$_v(u, v) result(res)
        type(${dual_type}$), intent(in) :: u(:,:), v(:)
        type(${dual_type}$) :: res(size(u,1))
        integer :: i

        res%f = matmul(u%f, v%f)
        do i = 1, size(res(1)%g)
            res%g(i) = matmul(u%g(i), v%f) + matmul(u%f, v%g(i))
        end do

    end function
    ${purity}$  function matmul__v_${dual_sn}$(u, v) result(res)
        type(${dual_type}$), intent(in) :: u(:), v(:,:)
        type(${dual_type}$) :: res(size(v, 2))
        integer::i

        res%f = matmul(u%f, v%f)
        do i = 1, size(res(1)%g)
            res%g(i) = matmul(u%g(i), v%f) + matmul(u%f, v%g(i))
        end do

    end function
  #:endfor
#:endif 
#:if "max" in interface_list
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    ${elemental_purity}$  function max__${dual_sn}$_${dual_sn}$(val1, val2) result(res)
        type(${dual_type}$), intent(in) :: val1, val2
        type(${dual_type}$) :: res

        if (val1%f > val2%f) then
            res = val1
        else
            res = val2
        endif

    end function
    ${elemental_purity}$  function max__${dual_sn}$_i(u, i) result(res)
        type(${dual_type}$), intent(in) :: u
        integer, intent(in) :: i
        type(${dual_type}$) :: res

        if (u%f > i) then
            res = u
        else
            res = i
        endif

    end function
    ${elemental_purity}$  function max__${dual_sn}$_r(u, r) result(res)
        type(${dual_type}$), intent(in) :: u
        real(${real_kind}$), intent(in) :: r
        type(${dual_type}$) :: res

        if (u%f > r) then
            res = u
        else
            res = r
        endif

    end function
     ${elemental_purity}$  function max__r_${dual_sn}$(n, u) result(res)
        real(${real_kind}$), intent(in) :: n
        type(${dual_type}$), intent(in) :: u
        type(${dual_type}$) :: res

        if (u%f > n) then
            res = u
        else
            res = n
        endif

    end function
  #:endfor
#:endif 
#:if "dmax1" in interface_list 
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    ${elemental_purity}$  function dmax1__${dual_sn}$_${dual_sn}$(val1, val2, val3, val4,val5) result(res)
        type(${dual_type}$), intent(in) :: val1, val2
        type(${dual_type}$), intent(in), optional :: val3, val4,val5
        type(${dual_type}$) :: res

        if (val1%f > val2%f) then
            res = val1
        else
            res = val2
        endif
        if(present(val3))then
           if(res%f < val3%f) res = val3
        endif
        if(present(val4))then
           if(res%f < val4%f) res = val4
        endif
        if(present(val5))then
           if(res%f < val5%f) res = val5
        endif

    end function
  #:endfor
#:endif 
#:if "maxval" in interface_list 
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    ${purity}$  function maxval__${dual_sn}$(u) result(res)
        type(${dual_type}$), intent(in) :: u(:)
        integer :: iloc(1)
        type(${dual_type}$) :: res

        iloc=maxloc(u%f)
        res=u(iloc(1))

    end function
  #:endfor
#:endif 
#:if "min" in interface_list 
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    ${elemental_purity}$  function min__${dual_sn}$_${dual_sn}$(val1, val2) result(res)
        type(${dual_type}$), intent(in) :: val1, val2
        type(${dual_type}$) :: res

        if (val1%f < val2%f) then
            res = val1
        else
            res = val2
        endif

    end function
    ${elemental_purity}$  function min__${dual_sn}$_r(u, r) result(res)
        type(${dual_type}$), intent(in) :: u
        real(${real_kind}$), intent(in) :: r
        type(${dual_type}$) :: res

        if (u%f < r) then
            res = u
        else
            res = r
        endif

    end function
  #:endfor
#:endif 
#:if "dmin1" in interface_list 
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    ${elemental_purity}$  function dmin1__${dual_sn}$_${dual_sn}$(val1, val2, val3, val4) result(res)
        type(${dual_type}$), intent(in) :: val1, val2
        type(${dual_type}$), intent(in), optional :: val3, val4
        type(${dual_type}$) :: res

        if (val1%f < val2%f) then
            res = val1
        else
            res = val2
        endif
        if(present(val3))then
           if(res%f > val3%f) res = val3
        endif
        if(present(val4))then
           if(res%f > val4%f) res = val4
        endif

    end function
  #:endfor
#:endif 
#:if "minval" in interface_list 
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    ${purity}$  function minval__${dual_sn}$(u) result(res)
        type(${dual_type}$), intent(in) :: u(:)
        integer :: iloc(1)
        type(${dual_type}$) :: res

        iloc=minloc(u%f)
        res=u(iloc(1))

    end function
  #:endfor
#:endif 
#:if "nint" in interface_list 
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    ${elemental_purity}$  function nint__${dual_sn}$(u) result(res)
        type(${dual_type}$), intent(in) :: u
        integer :: res

        res=nint(u%f)

    end function
  #:endfor
#:endif 
#:if "sign" in interface_list 
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    ${elemental_purity}$  function sign__${dual_sn}$_${dual_sn}$(val1, val2) result(res)
        type(${dual_type}$), intent(in) :: val1, val2
        type(${dual_type}$) :: res

        if (val2%f < 0.0_${real_kind}$) then
            res = -abs(val1)
        else
            res =  abs(val1)
        endif

     end function
    ${elemental_purity}$  function sign__r_${dual_sn}$(val1, val2) result(res)
        real(${real_kind}$), intent(in) :: val1
        type(${dual_type}$), intent(in) :: val2
        type(${dual_type}$) :: res

        if (val2%f < 0.0_${real_kind}$) then
            res = -abs(val1)
        else
            res = abs(val1)
        endif

     end function
  #:endfor
#:endif 
#:if "sin" in interface_list or "dsin" in interface_list
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    ${elemental_purity}$  function sin__${dual_sn}$(u) result(res)
        type(${dual_type}$), intent(in) :: u
        type(${dual_type}$) :: res

        res%f = sin(u%f)
        res%g = cos(u%f) * u%g

    end function
  #:endfor
#:endif 
#:if "tan" in interface_list or "dtan" in interface_list
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    ${elemental_purity}$  function tan__${dual_sn}$(u) result(res)
        type(${dual_type}$), intent(in) :: u
        type(${dual_type}$) :: res

        res%f = tan(u%f)
        res%g = u%g / cos(u%f)**2

    end function
  #:endfor
#:endif 
#:if "sqrt" in interface_list
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    $:sqrt_d(dual_type, dual_sn, real_kind, is_pure, test_coverage, stringent)
  #:endfor
  #:for hdual_type, hdual_sn in zip(hdual_types, hdual_shortnames)
    $:sqrt_hd(hdual_type, hdual_sn, real_kind, is_pure, test_coverage, stringent)
  #:endfor
#:endif 
#:if "sum" in interface_list 
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    ${purity}$  function sum__${dual_sn}$(u) result(res)
        type(${dual_type}$), intent(in) :: u(:)
        type(${dual_type}$) :: res
        integer :: i

        res%f = sum(u%f)
        do i = 1, size(res%g)
            res%g(i) = sum(u%g(i))
        end do

    end function
  #:endfor
#:endif 
#:if "maxloc" in interface_list 
  #:for dual_type, dual_sn in zip(dual_types, dual_shortnames)
    ${purity}$  function maxloc__${dual_sn}$(array) result(ind)
        type(${dual_type}$), intent(in) :: array(:)
        integer :: ind(1)

        ind = maxloc(array%f)

    end function
  #:endfor
#:endif 
#:enddef 

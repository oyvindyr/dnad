#:mute

#:include '../src/macros/dnad_dual_types.fypp'
#:include '../src/macros/dnad_overload_interfaces.fypp'
#:include '../src/macros/dnad_overload_implementations.fypp'
#:include '../src/macros/dnad_initialize.fypp'
#:include '../src/macros/dnad_getters.fypp'
#:include '../src/macros/dnad_jacobi.fypp'
#:include '../src/macros/dnad_chain_duals.fypp'
#:include '../src/macros/dnad_display.fypp'


#! When stringent = False, the dnad functions assume that the derivative exists. It is does not exist (for instance for abs(0.0))
#! the result will be undefined. When stringent = True (or not defined) some logic is included in the mathematical functions to
#! determine if the derivative exists of not, and call a function set_NaN() when it does not exist. Using stringent = False is 
#! believed to be faster.
#:set stringent = False

#! selected_interfaces is a comma separated list or "--all"
#:set selected_interfaces = "=,+,-,*,**,log,sqrt"
#!set selected_interfaces = "--all"

#:endmute
module test_hdual_chain_mod
    use, intrinsic :: iso_fortran_env, only: dp => real64
    implicit none

    private
    public :: test_hdual_chain

    integer, parameter :: ny = 2
    integer, parameter :: nx = 3

  #:if stringent
    ! Workaround to make pure functions produce NaN's deliberately:
    real(dp) :: negative_one = -1.0_dp
  #:endif
    
    @:dnad_dual_type(y:h, dp, ny, sequence)
    @:dnad_dual_type(x:h, dp, nx, sequence)

    interface  f_of_y
        module procedure f_of_y__x ! Function of y, with derivatives wrt x
        module procedure f_of_y__y ! Function of y, with derivatives wrt y
    end interface

    #:set dual_type_specs = ['x:h', 'y:h']
    $:dnad_initialize_interface(dual_type_specs)
    $:dnad_display_interface(dual_type_specs)
    $:dnad_getter_interfaces(dual_type_specs)
    $:dnad_jacobi_interfaces(dual_type_specs)
    $:dnad_chain_duals_interface(["y->x:h"])
    $:dnad_overload_interfaces(dual_type_specs, selected_interfaces)
    
contains

    function test_hdual_chain() result(is_ok)
        logical :: is_ok
           
        real(dp), parameter :: abs_tol = 1.0e-14_dp

        integer :: i

        real(dp) :: x(nx)

        type(hdual__y_t) :: fy
        type(hdual__y_t) :: yy(ny)

        type(hdual__x_t) :: fx
        type(hdual__x_t) :: yx(ny)
        type(hdual__x_t) :: xx(nx)

        type(hdual__x_t) :: fx_chain

        ! random x
        call random_number(x)
        x = 5*x + 0.1_dp

        ! Initialize xx, representing x as a function of x, with derivatives wrt x
        call initialize(xx, x)
        
        ! y as a function of x, with derivatives wrt x
        yx = y_of_x(xx)

        ! y as a function of y, with derivatives wrt y
        call initialize(yy, fvalue(yx))

        ! f as a function of y, with derivatives wrt y
        fy = f_of_y(yy)

        ! f as a function of x, with derivatives wrt x
        fx_chain = chain_duals(fy, yx)

        ! Same as fx_chain, but computed directly
        fx = f_of_x(xx)

        print*," "
        call display(yy, "yy")
        print*," "
        call display(fy, "fy")
        print*," "
        call display(fx, fx_chain, "fx", "fx_chain")


        print*," "
        call display(yx, "yx")
        print*," "
        call display(jacobi_tr_d(yx), "yxjt")

        is_ok = .true.
        if (abs(fvalue(fx_chain) - fvalue(fx)) > abs_tol) then
            is_ok = .false.
        end if
        if (maxval(abs(gradient(fx_chain) - gradient(fx))) > abs_tol) then
            is_ok = .false.
        end if
        if (maxval(abs(fx_chain%h - fx%h)) > abs_tol) then
            is_ok = .false.
        end if

        


    end function

  #:for vd in ['x', 'y']
    function f_of_y__${vd}$(y) result(f)
        type(hdual__${vd}$_t), intent(in) :: y(ny)
        type(hdual__${vd}$_t) :: f
        f = sqrt(y(1))*log(y(2))
    end function
  #:endfor

    function y_of_x(x) result(y)
        type(hdual__x_t), intent(in) :: x(nx)
        type(hdual__x_t) :: y(ny)
        y(1) = sqrt(x(1)**2 + x(2)**2)
        y(2) = x(3)
    end function

    function f_of_x(x) result(f)
        type(hdual__x_t), intent(in) :: x(nx)
        type(hdual__x_t) :: f
        f = f_of_y(y_of_x(x))
    end function

    $:dnad_initialize_implementations(dual_type_specs, "dp")
    $:dnad_display_implementations(dual_type_specs)
    $:dnad_getter_implementations(dual_type_specs, "dp")
    $:dnad_jacobi_implementations(dual_type_specs, "dp")
    $:dnad_chain_duals_implementations(["y->x:h"], "dp")
    $:dnad_overload_implementations(dual_type_specs, "dp", selected_interfaces=selected_interfaces, stringent=stringent)
    
  #:if stringent
    ! Workaround to make pure functions produce NaN's deliberately:
    elemental function set_NaN() result(res)
        real(dp) :: res
        res = sqrt(negative_one)
    end function set_NaN
  #:endif
    
end module
#:mute

#:include '../src/macros/dnad_dual_types.fypp'
#:include '../src/macros/dnad_overload_interfaces.fypp'
#:include '../src/macros/dnad_overload_implementations.fypp'
#:include '../src/macros/dnad_initialize.fypp'
#:include '../src/macros/dnad_getters.fypp'


#! When stringent = False, the dnad functions assume that the derivative exists. It is does not exist (for instance for abs(0.0))
#! the result will be undefined. When stringent = True (or not defined) some logic is included in the mathematical functions to
#! determine if the derivative exists of not, and call a function set_NaN() when it does not exist. Using stringent = False is 
#! believed to be faster.
#:set stringent = False

#! selected_interfaces is a comma separated list or "--all"
#:set selected_interfaces = "=,+,*,**,log,sqrt"

#:endmute
module example_hdual_mod
    use, intrinsic :: iso_fortran_env, only: dp => real64
    implicit none

    private
    public :: example_hdual

    integer, parameter :: num_deriv = 3 ! Number of derivatives

  #:if stringent
    ! Workaround to make pure functions produce NaN's deliberately:
    real(dp) :: negative_one = -1.0_dp
  #:endif
    
    #! Inject the dual type definitions
    #!  Note: Calling a macro with @ will convert all arguments to strings
    @:dnad_dual_type(uvw:h, dp, num_deriv)

    #! Inject interfaces
    #!  Note: Calling a macro with $ will send in python variables as arguments
    $:dnad_initialize_interface(["uvw:h"])
    $:dnad_getter_interfaces(["uvw:h"])
    $:dnad_overload_interfaces(selected_interfaces, hdual_shortnames = ['hd'])
    
contains

    subroutine example_hdual()

        ! Number of function evaluations to perform:
        integer, parameter :: nval = 2

        integer :: i
        real(dp) :: r

        type(hdual__uvw_t), dimension(nval) :: uhd, vhd, whd, fhd

        real(dp), dimension(num_deriv, num_deriv) :: hessian1, hessian2 

        ! Initialize dual inputs:
        do i = 1, nval

            call random_number(r)
            call initialize(uhd(i), r, 1)

            call random_number(r)
            call initialize(vhd(i), r, 2)

            call random_number(r)
            call initialize(whd(i), r, 3)

        end do

        ! Function to compute value, gradient and Hessian for:
        fhd = test_function_hdual(uhd, vhd, whd)

        ! Extract full hessian from lower triangular:
        hessian1 = hessian(fhd(1))
        hessian2 = hessian(fhd(2))


        print *, "Function value 1:"
        print *, fvalue(fhd(1))
        print *, "Function value 2:"
        print *, fvalue(fhd(2))
        print *, "Gradient 1:"
        print *, gradient(fhd(1))
        print *, "Gradient 2:"
        print *, gradient(fhd(2))
        print *, "Hessian 1:"
        do i = 1, num_deriv
            print *, hessian1(i, :)
        end do
        print *, "Hessian 2:"
        do i = 1, num_deriv
            print *, hessian2(i, :)
        end do

      
    end subroutine

    elemental function test_function_hdual(u, v, w) result(f)
        type(hdual__uvw_t), intent(in) :: u, v, w
        type(hdual__uvw_t) :: f
        f = sqrt(u)*log(v) + u**w
    end function

    #! Inject overloaded function implementations
    $:dnad_initialize_implementations(["uvw:h"], "dp")
    $:dnad_getter_implementations(["uvw:h"], "dp")
    $:dnad_overload_implementations("dp", &
            hdual_types = ['hdual__uvw_t'], hdual_shortnames = ['hd'], hdual_sizes = ['num_deriv'], &
            selected_interfaces=selected_interfaces, &
            stringent=stringent)
    
  #:if stringent
    ! Workaround to make pure functions produce NaN's deliberately:
    elemental function set_NaN() result(res)
        real(dp) :: res
        res = sqrt(negative_one)
    end function set_NaN
  #:endif
    
end module